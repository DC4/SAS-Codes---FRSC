
PROC DELETE DATA=_ALL_;RUN;

**********************************************************
		LR_Global_CNP_30DAY Global Credit
				
**********************************************************;

OPTIONS COMPRESS=YES;

LIBNAME CR "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Falcon 6.4 Monthly Data\Credit";
LIBNAME INC "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Daily Data\CR";

%MACRO DR(CNTY,MTH);
DATA FALT002_A;
     SET  CR.falt002_&CNTY._cr_&MTH.2021 ;
     WHERE TRN_AUTH_POST = "A" AND CRD_CLNT_ID IN ("SC_CCMSSG_CR" "SC_CCMSMY_CR" "SC_C400AE_CR" )
	AND DATE1 GE "23JUN2021"D;
     FORMAT TRAN_DATE DATE9. TIME TIME8.;
     TRAN_DATE = INPUT(SUBSTR(TRN_DT,1,2) || SUBSTR(TRN_DT,4,3) || '20' || SUBSTR(TRN_DT,8,2),DATE9.);
     ATTRIB AUTH_DTTM FORMAT=DATETIME19.;
     AUTH_DTTM=DHMS(TRAN_DATE,SUBSTR(TRN_DT,11,2),SUBSTR(TRN_DT,14,2),SUBSTR(TRN_DT,17,2)); 
     MCC = INPUT(SIC_CD,BEST.);
         ATMONUS=(COMPRESS(MER_ID)||COMPRESS(ACCT_NBR));
/*         IF AUTH_DTTM GE "26FEB2021:22:15:00"DT;*/
         TIME = TIMEPART(AUTH_DTTM);
		RUN;

PROC SORT DATA=FALT002_A NODUPKEY; BY FI_TRANSACTION_ID; RUN;
/*PROC SORT DATA=FALT002_A ; BY ATMONUS AUTH_DTTM; RUN;*/

PROC APPEND DATA=FALT002_A BASE=FALT002 FORCE;RUN;

PROC DELETE DATA=FALT002_A;RUN;
%MEND;

/*%DR(SG,JUN);*/
/*%DR(SG,JUL);*/
/*%DR(SG,AUG);*/
%DR(SG,SEP);
%DR(SG,OCT);


/*%DR(MY,JUN);*/
/*%DR(MY,JUL);*/
/*%DR(MY,AUG);*/
%DR(MY,SEP);
%DR(MY,OCT);

/*%DR(AE,JUN);*/
/*%DR(AE,JUL);*/
/*%DR(AE,AUG);*/
%DR(AE,SEP);
%DR(AE,OCT);

/*Wed 6/23/2021 3:19 PM*/

PROC SORT DATA=FALT002 ; BY ATMONUS AUTH_DTTM;WHERE AUTH_DTTM GE '23JUN2021:17:49:00'DT; RUN;


DATA FALT002;
     SET FALT002;
     BY ATMONUS AUTH_DTTM;

     FORMAT ATMONUS_FIRST_TXN_DATE DATETIME19. ATMONUS_FIRST_APPROVAL_DATE DATETIME19.;
	 RETAIN ATMONUS_FIRST_TXN_DATE ATMONUS_FIRST_APPROVAL_DATE;

     IF FIRST.ATMONUS THEN DO;
           ATMONUS_FIRST_TXN_DATE = 0;
		   ATMONUS_FIRST_APPROVAL_DATE = 0;
     END;

     IF TRN_AUTH_POST = "A" and MER_ID NE "" THEN DO;
           IF ATMONUS_FIRST_TXN_DATE = 0 THEN DO;
                ATMONUS_FIRST_TXN_DATE = AUTH_DTTM;
           END;
           IF ATMONUS_FIRST_APPROVAL_DATE = 0 AND TRN_TYP IN ("C" "M" "P") AND (AUTH_DECISION_XCD = "A" OR DECI_CD_ORIG="A") 
		   AND TRN_AMT GT 0 THEN DO;
                ATMONUS_FIRST_APPROVAL_DATE = AUTH_DTTM;
           END;
     END;


RUN;

%MACRO UDV(CUM_INTERVAL=, CUMULATIVE_CONDITION =, VAR_NAME=);

DATA FALT002;
     SET FALT002;
     BY ATMONUS AUTH_DTTM;

     FORMAT TIME_RESET_&VAR_NAME. DATE9.;

     RETAIN CUM_COUNT_&VAR_NAME. TIME_RESET_&VAR_NAME. CUM_AMOUNT_&VAR_NAME. ATMONUS_30DAY_MAX_CNT;

     IF FIRST.ATMONUS THEN DO;
           TIME_RESET_&VAR_NAME. = 0;
           CUM_COUNT_&VAR_NAME. = 0;
           CUM_AMOUNT_&VAR_NAME. = 0;
		   ATMONUS_30DAY_MAX_CNT = 0;
     END;

     IF &CUMULATIVE_CONDITION. THEN DO;
           IF TRAN_DATE LE (TIME_RESET_&VAR_NAME. + &CUM_INTERVAL.) THEN DO;
                CUM_COUNT_&VAR_NAME. + 1;
                CUM_AMOUNT_&VAR_NAME. + TRN_AMT;
           END;
           ELSE DO;
		   		ATMONUS_30DAY_MAX_CNT = MAX(ATMONUS_30DAY_MAX_CNT,CUM_COUNT_&VAR_NAME.);
                TIME_RESET_&VAR_NAME. = TRAN_DATE;
                CUM_COUNT_&VAR_NAME. = 1;
                CUM_AMOUNT_&VAR_NAME. = TRN_AMT;
           END;
     END;


RUN;
%MEND; 

%UDV(CUM_INTERVAL        = 30,                                          
     CUMULATIVE_CONDITION= %STR(TRN_AUTH_POST = "A" and MER_ID ne '' and TRN_TYP IN ("C" "M" "P") and
(AUTH_DECISION_XCD = "A" or DECI_CD_ORIG = "A") and TRN_AMT > 0),
     VAR_NAME            = ATMONUS_30DAY);


DATA FALT002_1;
SET FALT002;
BY ATMONUS AUTH_DTTM;
RETAIN ATMONUS_30DAY_TRIGGERED;
IF FIRST.ATMONUS THEN ATMONUS_30DAY_TRIGGERED=0;
IF
((TRN_AUTH_POST = "A" and AUTH_DECISION_XCD = "A" and
TRN_TYP IN ("C" "M" "P") and TRN_POS_ENT_CD IN ("E" "K" "G") and
SUBSTR(USR_IND_4,1,2) NE "Y2" and TRN_AMT > 0 and ATMONUS_30DAY_TRIGGERED = 0 AND
(AUTH_DTTM - ATMONUS_FIRST_APPROVAL_DATE) <= (30*86400) AND
SIC_CD NOT IN ("6011" "4121")) 
AND 
(
(CRD_CLNT_ID = "SC_CCMSSG_CR"  and
 MER_CNTY_CD NE "702" and  CUM_COUNT_ATMONUS_30DAY >= 15 ) or
(CRD_CLNT_ID = "SC_CCMSMY_CR"  and
 MER_CNTY_CD NE "458" and  CUM_COUNT_ATMONUS_30DAY >= 15 ) or
 (CRD_CLNT_ID = "SC_C400AE_CR"  and
 MER_CNTY_CD NE "784" and  CUM_COUNT_ATMONUS_30DAY >= 15 ) 
) 
) then do;
LR_Global_CNP_30DAY="Y";
ATMONUS_30DAY_TRIGGERED = 1;
end;
RUN;


DATA TXN_DATA;
SET FALT002_1;
WHERE (LR_Global_CNP_30DAY="Y" and date1 GE "17OCT2021"D);
RUN;


DATA RULE_DATA;
     SET INC.FALT003_CR_17OCT ;
       WHERE UPCASE(RULE_NAME_STRG) IN ('LR_GLOBAL_CNP_30DAY') and date1 GE "17OCT2021"D AND
CLIENT_XID IN ("SC_CCMSSG_CR" "SC_CCMSMY_CR" "SC_C400AE_CR" );
RUN;

PROC SORT DATA=RULE_DATA NODUPKEY; BY RULE_NAME_STRG FI_TRANSACTION_ID; RUN;

PROC FREQ DATA=TXN_DATA;TITLE "LR_Global_CNP_30DAY SAS";
TABLE CRD_CLNT_ID*DATE1/NOCOL NOROW NOPERCENT NOCUM; RUN; 
PROC FREQ DATA=RULE_DATA; TITLE "LR_Global_CNP_30DAY FALCON";
TABLE CLIENT_XID*DATE1/NOCOL NOROW NOPERCENT NOCUM; RUN; 



/* Writing Outout to Excel - For Audit*/

ods listing;
ods excel file='C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\SAS\Outputs\LR_GLOBAL_CNP_30DAY Global Credit.xlsx'
 options(sheet_interval="NONE");
 ods excel options(sheet_name="OUTPUT");
PROC FREQ DATA = TXN_DATA;TITLE "LR_GLOBAL_CNP_30DAY  SAS";
TABLE CRD_CLNT_ID*DATE1/NOCOL NOROW NOPERCENT NOCUM; RUN; 
PROC FREQ DATA = RULE_DATA; TITLE "LR_GLOBAL_CNP_30DAY  FALCON";
TABLE CLIENT_XID*DATE1/NOCOL NOROW NOPERCENT NOCUM; RUN;
ods excel close;
ods listing close;

PROC EXPORT DATA= TXN_DATA
outfile= "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\SAS\Outputs\LR_GLOBAL_CNP_30DAY Global Credit.xlsx"
dbms=xlsx replace;
sheet="TXN_DATA";
run;

PROC EXPORT DATA= RULE_DATA
outfile= "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\SAS\Outputs\LR_GLOBAL_CNP_30DAY Global Credit.xlsx"
dbms=xlsx replace;
sheet="RULE_DATA";
run;