
**********************************************************
		MR_BIN_Attack_Loc_CNP_Test Global Credit	 
				
**********************************************************;

proc delete data = BLACKLISTED_MERCHANTS; run;

%macro ch(tnt, start, end);

%do SHEET = &START. %to &END. %by 1;

proc import datafile="C:\Users\1586087\Downloads\data (&SHEET.).xls" out=x_&SHEET. dbms=xls replace; 
namerow=2;
startrow=3;
run;

data x_&SHEET.;
set x_&SHEET.;
TNT="&tnt.";
run;

proc append data=x_&SHEET. base=BLACKLISTED_MERCHANTS force; run;

%end;

%mend;

%ch(RAC_IN,14, 16);
%ch(CNTRY,17, 19);



%MACRO HOTLIST(tnt,HT);

PROC IMPORT DATAFILE="C:\Users\1586087\Downloads\BLACKLISTED MERCHANTS  &HT..XLS"
OUT=&HT. DBMS=XLS REPLACE;
namerow=2;
startrow=3;
RUN;

data &HT.;
set &HT.;
TNT="&tnt.";
run;

PROC APPEND BASE=BLACKLISTED_MERCHANTS DATA=&HT. FORCE;RUN;

%MEND;

%HOTLIST(RAC_SG,part1);
%HOTLIST(RAC_SG,part2);
%HOTLIST(RAC_SG,part3);
%HOTLIST(RAC_SG,part4);


LIBNAME INC "C:\Users\1586087\Documents\Daily_datasets\Credit";


DATA FALT002;
	SET INC.falt002_cr_06AUG
		INC.falt002_cr_07AUG
		inc.falt002_cr_08AUG;
     WHERE TRN_AUTH_POST = "A" AND MER_ID NE "" AND TRN_POS_ENT_CD IN ("E" "K" "G")   AND 
 CRD_CLNT_ID NOT IN ("SC_C400BD_CR" "SC_CCMSTW_CR" "SC_CCMSHK_CR" "SC_PMTHK_CR") and
	 	((CRD_CLNT_ID = "SC_C400AE_CR" and MER_CNTY_CD EQ "784" ) or
			(CRD_CLNT_ID = "SC_C400BH_CR" and MER_CNTY_CD EQ "048" ) or 
			(CRD_CLNT_ID = "SC_C400BD_CR" and MER_CNTY_CD EQ "050" ) or 
			(CRD_CLNT_ID = "SC_CCMSBN_CR" and MER_CNTY_CD EQ "096" ) or
			(CRD_CLNT_ID = "SC_CCMSHK_CR" and MER_CNTY_CD EQ "344" ) or
			(CRD_CLNT_ID = "SC_CCMSTW_CR" and MER_CNTY_CD EQ "158" ) or
			(CRD_CLNT_ID = "SC_C400BW_CR" and MER_CNTY_CD EQ "072" ) or
			(CRD_CLNT_ID = "SC_C400GH_CR" and MER_CNTY_CD EQ "288" ) or
			(CRD_CLNT_ID = "SC_CCMSID_CR" and MER_CNTY_CD EQ "360" ) or
			(CRD_CLNT_ID = "SC_CCMSIN_CR" and MER_CNTY_CD EQ "356" ) or
			(CRD_CLNT_ID = "SC_C400JE_CR" and MER_CNTY_CD EQ "832" ) or
			(CRD_CLNT_ID = "SC_C400JO_CR" and MER_CNTY_CD EQ "400" ) or
			(CRD_CLNT_ID = "SC_C400KE_CR" and MER_CNTY_CD EQ "404" ) or
			(CRD_CLNT_ID = "SC_C400LK_CR" and MER_CNTY_CD EQ "144" ) or
			(CRD_CLNT_ID = "SC_CCMSMY_CR" and MER_CNTY_CD EQ "458" ) or
			(CRD_CLNT_ID = "SC_C400NG_CR" and MER_CNTY_CD EQ "566" ) or
			(CRD_CLNT_ID = "SC_C400NP_CR" and MER_CNTY_CD EQ "524" ) or
			(CRD_CLNT_ID = "SC_CCMSSG_CR" and MER_CNTY_CD EQ "702" ) or
			(CRD_CLNT_ID = "SC_C400VN_CR" and MER_CNTY_CD EQ "704" ) or
			(CRD_CLNT_ID = "SC_C400ZM_CR" and MER_CNTY_CD EQ "894" ));
	 MER_ID_BIN = STRIP(MER_ID) || STRIP(SUBSTR(ACCT_NBR,1,6));
     FORMAT TRAN_DATE DATE9.;
     TRAN_DATE = INPUT(SUBSTR(TRN_DT,1,2) || SUBSTR(TRN_DT,4,3) || '20' || SUBSTR(TRN_DT,8,2),DATE9.);
     ATTRIB AUTH_DTTM FORMAT=DATETIME19.;
     AUTH_DTTM=DHMS(TRAN_DATE,SUBSTR(TRN_DT,11,2),SUBSTR(TRN_DT,14,2),SUBSTR(TRN_DT,17,2)); 
     MCC = INPUT(SIC_CD,BEST.);
	 COUNTRY1 = SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4,2);
RUN;


PROC SORT DATA=FALT002 NODUPKEY; BY FI_TRANSACTION_ID; RUN;

PROC SORT DATA=FALT002 OUT=FALT002_2; BY MER_ID_BIN AUTH_DTTM ; RUN;


********** CREATING UDV : 15 MIN COUNTERS ***************;


%MACRO UDV(CUM_INTERVAL=, CUMULATIVE_CONDITION =, VAR_NAME=);

DATA FALT002_2;
     SET FALT002_2;
     BY MER_ID_BIN AUTH_DTTM;

     FORMAT TIME_RESET_&VAR_NAME. DATETIME19.;

     RETAIN CUM_COUNT_&VAR_NAME. TIME_RESET_&VAR_NAME. CUM_AMOUNT_&VAR_NAME.;

     IF FIRST.MER_ID_BIN THEN DO;
           TIME_RESET_&VAR_NAME. = 0;
           CUM_COUNT_&VAR_NAME. = 0;
           CUM_AMOUNT_&VAR_NAME. = 0;
     END;

     IF &CUMULATIVE_CONDITION. THEN DO;
           IF AUTH_DTTM LE (TIME_RESET_&VAR_NAME. + &CUM_INTERVAL.) THEN DO;
                CUM_COUNT_&VAR_NAME. + 1;
                CUM_AMOUNT_&VAR_NAME. + TRN_AMT;
           END;
           ELSE DO;
                TIME_RESET_&VAR_NAME. = AUTH_DTTM;
                CUM_COUNT_&VAR_NAME. = 1;
                CUM_AMOUNT_&VAR_NAME. = TRN_AMT;
           END;
     END;


RUN;
%MEND; 


%UDV(CUM_INTERVAL        = 900,                                          
     CUMULATIVE_CONDITION= %STR(TRN_AUTH_POST = "A" AND TRN_POS_ENT_CD  IN ("E" "K" "G") AND MER_ID NE "" AND DECI_CD_ORIG="N"),
     VAR_NAME            = CNP_N);

%UDV(CUM_INTERVAL        = 900,                                          
     CUMULATIVE_CONDITION= %STR(TRN_AUTH_POST = "A" AND TRN_POS_ENT_CD  IN ("E" "K" "G") AND MER_ID NE "" AND DECI_CD_ORIG="C"),
     VAR_NAME            = CNP_C);

%UDV(CUM_INTERVAL        = 900,                                          
     CUMULATIVE_CONDITION= %STR(TRN_AUTH_POST = "A" AND TRN_POS_ENT_CD  IN ("E" "K" "G") AND MER_ID NE "" AND DECI_CD_ORIG="E"),
     VAR_NAME            = CNP_E);

%UDV(CUM_INTERVAL        = 900,                                          
     CUMULATIVE_CONDITION= %STR(TRN_AUTH_POST = "A" AND TRN_POS_ENT_CD  IN ("E" "K" "G") AND MER_ID NE "" AND DECI_CD_ORIG="D"),
     VAR_NAME            = CNP_D);


PROC SQL;
CREATE TABLE MIDS_HOTLIST_CREDIT_Test AS
SELECT A.*, B.STATUS, B.VALUE FROM 
FALT002_2 AS A
LEFT JOIN 
BLACKLISTED_MERCHANTS AS B ON
COMPRESS(A.MER_ID) = COMPRESS(B.NAME)
WHERE NOT(B.STATUS NE "" AND B.TNT = "" AND INDEX(B.VALUE,STRIP(A.COUNTRY1))) AND
TNT="CNTRY";
QUIT;


PROC SQL;
CREATE TABLE MIDS_HOTLIST_CREDIT_Test2 AS
SELECT * FROM MIDS_HOTLIST_CREDIT_Test 
WHERE
((CRD_CLNT_ID = "SC_CCMSMY_CR" AND COMPRESS(MER_ID) NOT IN 
(SELECT DISTINCT STRIP(NAME) FROM BLACKLISTED_MERCHANTS WHERE TNT="RAC_SG"))
OR
(CRD_CLNT_ID NOT IN ("SC_CCMSMY_CR" "SC_C400BD_CR" "SC_CCMSTW_CR" "SC_CCMSHK_CR" "SC_PMTHK_CR") AND COMPRESS(MER_ID) NOT IN 
(SELECT DISTINCT STRIP(NAME) FROM BLACKLISTED_MERCHANTS WHERE TNT="RAC_INDIA")))
;
QUIT;


DATA MR_BIN_Attack_Loc_CNP_Test_SAS;

	SET MIDS_HOTLIST_CREDIT_Test2;

	**************** Decision Rule MR_BIN_Attack_Loc_CNP_Test *****************; 

	IF TRN_AUTH_POST = "A" AND TRN_TYP IN ("C"  "M"  "P") AND TRN_POS_ENT_CD IN ("E" "K" "G") AND
	MER_ID NE ""  AND CRD_CLNT_ID NOT IN ("SC_CCMSMY_CR" "SC_C400BD_CR" "SC_CCMSTW_CR" "SC_CCMSHK_CR" "SC_PMTHK_CR") AND 
	(
	(CUM_COUNT_CNP_N GT 100 ) OR 
	(CUM_COUNT_CNP_C GT 100 ) OR 
	(CUM_COUNT_CNP_E GT 100 ) OR 
	(CUM_COUNT_CNP_D GT 100 )
	)
	AND
	(
	 (AUTH_DTTM - TIME_RESET_CNP_N) =< 900 OR 
	 (AUTH_DTTM - TIME_RESET_CNP_C) =< 900 OR 
	 (AUTH_DTTM - TIME_RESET_CNP_E) =< 900 OR 
	 (AUTH_DTTM - TIME_RESET_CNP_D) =< 900
	)

	THEN MR_BIN_Attack_Loc_CNP_Test = "Y";

RUN;

DATA MR_BIN_Attack_Loc_CNP_Test;
	SET MR_BIN_Attack_Loc_CNP_Test_SAS;
	WHERE MR_BIN_Attack_Loc_CNP_Test = "Y" AND DATE1 GE "07AUG2021"D;
RUN;

DATA MR_BIN_Attack_Loc_CNP_Test_RL;
	SET INC.falt003_cr_07AUG
		INC.falt003_cr_08AUG;
WHERE UPCASE(RULE_NAME_STRG) IN ('MR_BIN_ATTACK_LOC_CNP_TEST') AND CLIENT_XID NOT IN 
("SC_C400BD_CR" "SC_CCMSTW_CR" "SC_CCMSHK_CR" "SC_PMTHK_CR") AND DATE1 GE "07AUG2021"D;
RUN;

PROC SORT DATA = MR_BIN_Attack_Loc_CNP_Test_RL NODUPKEY ;BY FI_TRANSACTION_ID; RUN;



PROC FREQ DATA=MR_BIN_Attack_Loc_CNP_Test; TITLE "MR_BIN_Attack_Loc_CNP_Test SAS"; 
TABLE CRD_CLNT_ID*DATE1/NOCOL NOROW NOPERCENT NOCUM; RUN;

PROC FREQ DATA=MR_BIN_Attack_Loc_CNP_Test_RL; TITLE "MR_BIN_Attack_Loc_CNP_Test FALCON HITS"; 
TABLE CLIENT_XID*DATE1/NOCOL NOROW NOPERCENT NOCUM; RUN;
