
PROC DELETE DATA = RULE_PERFORMENCE_CR; RUN;

****Place the updated FRD15 dataset inside the below mentioned folder***********************;

/*COMMENTS: CHANGE PATH*/

LIBNAME EXIST "C:\Users\1510806\Desktop\Jhilam\Falcon 6.4 Monthly Data";

OPTIONS COMPRESS=YES;


 %LET TOD = %SYSFUNC(TODAY());
 %LET MNTH1 = %SYSFUNC(INTNX(MONTH,&TOD.,-1),MONYY7.);
 %LET MNTH2 = %SYSFUNC(INTNX(MONTH,&TOD.,-2),MONYY7.);
 %LET MNTH3 = %SYSFUNC(INTNX(MONTH,&TOD.,-3),MONYY7.);
 %LET MNTH_1 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH1.,1,3)),%SYSFUNC(SUBSTR(&MNTH1.,6,2)) ));
 %LET MNTH_2 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH2.,1,3)),%SYSFUNC(SUBSTR(&MNTH2.,6,2)) ));
 %LET MNTH_3 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH3.,1,3)),%SYSFUNC(SUBSTR(&MNTH3.,6,2)) ));
 %PUT &MNTH1. &MNTH2. &MNTH3. &MNTH_1. &MNTH_2. &MNTH_3.;





********* CHECKING THE LIST OF BLACKLISTING RULES **********;



proc delete data = all_rules; run;

%macro name(nm);

FILENAME RAW "C:\Users\1510806\Desktop\Jhilam\Falcon Rules\Text Files\&nm. Tenant Rule Report.TXT"; 

DATA FST(drop=tmp TMP1 rule_name1) ;

INFILE RAW LRECL=136 TRUNCOVER IGNOREDOSEOF LRECL = 800 ;
input tmp $800.;
format product $200. rule_name $200.;
retain product rule_set rule_name rule_name1 mode QAV rule_type;
if  tmp = "" then delete;
TMP1 = LAG(TMP);


** QAV **;

if (index(compress(tmp,'"'),"LookupListValue(BLACKLIST")>0 or index(compress(tmp,'"'),"LookupList(BLACKLIST")>0)
and index(compress(tmp),"<>1") = 0 and index(compress(tmp),"=0") = 0 and index(compress(tmp),"not(Get") = 0 and index(compress(tmp),"not(GlobalLookup") = 0 and index(compress(tmp),"not(Lookup") = 0
then QAV="BLACKLIST";

** Decision **;

if index(tmp,"DECLINE")>0 then rule_type='RTD';
IF index(tmp,"TriggerCase(SERVICE)")>0 AND index(tmp1,"DECLINE")>0 then rule_type='RTD';
IF index(tmp,"ForceCase(SERVICE)")>0 AND index(tmp1,"DECLINE")=0 then rule_type='CC';
else if index(tmp,"TriggerCase(SERVICE)")>0 then rule_type='CC';

** Rule Name **;

if index(tmp,"Rule Name:")>0 then do; rule_name=substr(tmp,12,200); QAV = ""; rule_type = ""; end;
if index(tmp1,"Rule Name:")>0 then rule_name1=substr(tmp1,12,200);

** Mode **;

if rule_name1 = rule_name and index(tmp1,"Mode:")>0 then mode=compress(substr(tmp,1,50));

** Rule Set **;

if index(tmp,"Rule Set:")>0 then do; rule_set=substr(tmp,12,50); rule_name = ""; mode = ""; QAV = "";rule_type = ""; end;

** Product **;

if index(tmp,"Credit 25 Authorization-Posting")>0 or index(tmp,"Debit 25 Authorization-Posting")>0 
or index(tmp,"Account Information Summary 20")>0 or index(tmp,"Business Information Summary (BIS) 21")>0
or index(tmp,"PAN Information Summary 12")>0 or index(tmp,"Retail Banking Transactions 21")>0
or index(tmp,"Nonmonetary 20")>0 or index(tmp,"Customer Information Summary (CIS) 20")>0
or index(tmp,"Fraud Dispositions 15")>0 or index(tmp,"External Message 10")>0
then do; product=strip(tmp); rule_set = ""; rule_name = ""; mode = ""; QAV = ""; rule_type = ""; end;


run;


proc sort data=fst; by product rule_set rule_name DESCENDING rule_type;run;
PROC SORT DATA=FST NODUPKEY; BY product rule_set rule_name; RUN;

DATA &nm.;
SET FST;
FORMAT TENANT $50.;
TENANT = "&NM.";
if rule_name ne "" and STRIP(UPCASE(RULE_SET)) = "DECISION RULESET"	and QAV="BLACKLIST" AND STRIP(UPCASE(PRODUCT))  = "CREDIT 25 AUTHORIZATION-POSTING";
RUN;

proc append base=all_rules data=&nm. force;run;

/*proc export data=fst outfile="C:\Saravana\&nm..xlsx" dbms=xlsx replace;run;*/

%mend;

%name(GBL);
%name(TW);
%name(BD);
%name(ID);
%name(HK);


********* END OF CODE : CHECKING THE LIST OF BLACKLISTING RULES **********;






**********Change the date as per requirement (which all dates need to append)*******************;

OPTIONS MPRINT MLOGIC SYMBOLGEN;

%MACRO COP(CNTY);


/*COMMENTS: CHANGE PATH*/

LIBNAME CREDIT "C:\Users\1510806\Desktop\Jhilam\Falcon 6.4 Monthly Data\credit";

DATA FALT003;
	SET CREDIT.FALT003_&CNTY._CR_&MNTH1.
		CREDIT.FALT003_&CNTY._CR_&MNTH2.
		CREDIT.FALT003_&CNTY._CR_&MNTH3.;
	WHERE STRIP(SUBSTR(CLIENT_XID,LENGTH(CLIENT_XID)-4))="&CNTY._CR";
	COUNTRY = UPCASE(STRIP(SUBSTR(CLIENT_XID,LENGTH(CLIENT_XID)-4)));
RUN;

PROC SORT DATA=FALT003 OUT=RULE_&CNTY. NODUPKEY; BY FI_TRANSACTION_ID RULE_NAME_STRG ; 
WHERE UPCASE(SUBSTR(CREATED_DTTM,4,6)) IN ("&MNTH_1." "&MNTH_2." "&MNTH_3.") and upcase(rule_name_strg) in 
(
'HR_COUNTRY_3DS_BLACKLIST_IN'
'HR_COUNTRY_BLACKLIST_MER_SG'
'HR_COUNTRY_BLACKLISTING_ID'
'HR_COUNTRY_MIDS_IN'
'HR_GLOBAL_BLACKLIST_MIDS_HOTLIST_CREDIT'
'HR_GLOBAL_BLACKLISTED_TOKEN'
'HR_GLOBAL_BLACKLIST_ALL_TXNS'
'HR_GLOBAL_BLACKLIST_MIDS_SUPRESS'
'HR_GLOBAL_BLACKLISTING_N1_1'
'HR_GLOBAL_MERCHANT_MID'
'MR_CC_BLACKLIST_ONLY_SMS_TXNS'
'MR_CC_GLOBAL_BLACKLIST_ALL_TXNS'
'HR_COUNTRY_BLACKLIST_MER_IN'
'HR_GLOBAL_BLACKLIST_MIDS_HOTLIST_DEBIT'
'HR_GLOBAL_BLACKLISTING_MIDS'
'HR_GLOBAL_DEBIT_BLACKLIST_MID'
'HR_GLOBAL_BLACKLISTINGN1_TW'
'LR_CC_BLACKLIST_ONLY_SMS_TXNS'
'HR_COUNTRY_BLACKLIST_MER_ID'
'HR_COUNTRY_MID_BLACKLIST_HK'
);
RUN;

PROC SQL;
	CREATE TABLE FALT002 AS SELECT * FROM 

	((SELECT * FROM CREDIT.FALT002_&CNTY._CR_&MNTH1. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._CR" AND 
	TRN_AUTH_POST='A' )) OUTER UNION CORR

	(SELECT * FROM CREDIT.FALT002_&CNTY._CR_&MNTH2. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._CR" AND 
	TRN_AUTH_POST='A' )) OUTER UNION CORR

	(SELECT * FROM CREDIT.FALT002_&CNTY._CR_&MNTH3. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._CR" AND 
	TRN_AUTH_POST='A' )) );
QUIT;

PROC SORT DATA=FALT002 OUT=TXN_&CNTY.; BY FI_TRANSACTION_ID DESCENDING FRD_IND; 
WHERE UPCASE(SUBSTR(TRN_DT,4,6)) IN ("&MNTH_1." "&MNTH_2." "&MNTH_3."); RUN;
PROC SORT DATA=TXN_&CNTY. NODUPKEY; BY FI_TRANSACTION_ID;  RUN;

PROC SQL;
	CREATE TABLE TXN_DETAILS_&CNTY. AS SELECT * FROM RULE_&CNTY. AS A LEFT JOIN TXN_&CNTY. AS B ON 
	A.SCORE_CUSTOMER_ACCOUNT_XID=B.ACCT_NBR AND
	A.FI_TRANSACTION_ID=B.FI_TRANSACTION_ID;
QUIT;

PROC SORT DATA=EXIST.FRD15 OUT=FRD_&CNTY. NODUPKEY; 
	WHERE STRIP(SUBSTR(CLIENTIDFROMHEADER,LENGTH(CLIENTIDFROMHEADER)-4))="&CNTY._CR"; 
	BY PAN FITRANSACTIONIDREFERENCE; 
RUN;

PROC SQL;
CREATE TABLE TXN_DETAILS_FRD_&CNTY. AS
		SELECT A.*,
		  SUBSTR(CREATED_DTTM,1,9) AS DATE,
		  UPCASE(SUBSTR(CREATED_DTTM,4,6)) AS MONTH,
		  A.TRN_AMT AS AMOUNT,
		  B.FRAUDFLAG,
		  CASE 
			WHEN STRIP(UPCASE(A.FRD_IND)) IN ("Y" "D") OR B.FRAUDFLAG="1" THEN 1 
		  END AS FRAUD
FROM TXN_DETAILS_&CNTY. AS A LEFT JOIN FRD_&CNTY. AS B ON 
A.ACCT_NBR = B.PAN AND
A.FI_TRANSACTION_ID=B.FITRANSACTIONIDREFERENCE;
QUIT;

PROC APPEND DATA=TXN_DETAILS_FRD_&CNTY. BASE=RULE_PERFORMENCE_CR FORCE; RUN;

%MEND;

%COP(AE);
%COP(BD);
%COP(BH);
%COP(BN);
%COP(BW);
%COP(GH);
%COP(HK);
%COP(ID);
%COP(IN);
%COP(JE);
%COP(JO);
%COP(KE);
%COP(LK);
%COP(MY);
%COP(NG);
%COP(NP);
%COP(SG);
%COP(TW);
%COP(VN);
%COP(ZM);

PROC SORT DATA=RULE_PERFORMENCE_CR; BY FI_TRANSACTION_ID RULE_NAME_STRG; RUN;

DATA RULE_PERFORMENCE_CR_1;
LENGTH RULES $ 400 ;
SET RULE_PERFORMENCE_CR;
RETAIN RULES;
BY FI_TRANSACTION_ID RULE_NAME_STRG;
IF FIRST.FI_TRANSACTION_ID THEN RULES = STRIP(RULE_NAME_STRG);
ELSE RULES = STRIP(RULES) || ", " || STRIP(RULE_NAME_STRG);
IF LAST.FI_TRANSACTION_ID;

	IF SUBSTR(ACCT_NBR,1,6) IN ('411144'
								'421451'
								'469626'
								'470691') AND CRD_CLNT_ID = "SC_C400BD_CR" THEN TRN_AMT_LOCAL = TRN_AMT * 83.60;
	ELSE TRN_AMT_LOCAL = TRN_AMT;

	IF CRD_CLNT_ID IN ("SC_CCMSSG_CR" "SC_CCMSBN_CR") THEN USD = TRN_AMT/1.255698;
	ELSE IF CRD_CLNT_ID IN ("SC_CCMSMY_CR") THEN USD = TRN_AMT/3.221743;
	ELSE IF CRD_CLNT_ID IN ("SC_CCMSPH_CR") THEN USD = TRN_AMT/43.88082;
	ELSE IF CRD_CLNT_ID IN ("SC_CCMSTH_CR") THEN USD = TRN_AMT/32.675467;
	ELSE IF CRD_CLNT_ID IN ("SC_CCMSID_CR") THEN USD = TRN_AMT/11627.906977;
	ELSE IF CRD_CLNT_ID IN ("SC_CCMSIN_CR") THEN USD = TRN_AMT/63.75;
	ELSE IF CRD_CLNT_ID IN ("SC_CCMSTW_CR") THEN USD = TRN_AMT/30.116853;
	ELSE IF CRD_CLNT_ID IN ("SC_C400AE_CR") THEN USD = TRN_AMT/3.67;
	ELSE IF CRD_CLNT_ID IN ("SC_C400BH_CR") THEN USD = TRN_AMT/0.377132;
	ELSE IF CRD_CLNT_ID IN ("SC_C400BW_CR") THEN USD = TRN_AMT/10.2587;
	ELSE IF CRD_CLNT_ID IN ("SC_C400GH_CR") THEN USD = TRN_AMT/4.42718;
	ELSE IF CRD_CLNT_ID IN ("SC_C400JO_CR") THEN USD = TRN_AMT/0.708893;
	ELSE IF CRD_CLNT_ID IN ("SC_C400JE_CR") THEN USD = TRN_AMT/0.75;
	ELSE IF CRD_CLNT_ID IN ("SC_C400KE_CR") THEN USD = TRN_AMT/103.824;
	ELSE IF CRD_CLNT_ID IN ("SC_C400LK_CR") THEN USD = TRN_AMT/153.05;
	ELSE IF CRD_CLNT_ID IN ("SC_C400NG_CR") THEN USD = TRN_AMT/365.467;
	ELSE IF CRD_CLNT_ID IN ("SC_C400NP_CR") THEN USD = TRN_AMT/102.69;
	ELSE IF CRD_CLNT_ID IN ("SC_C400VN_CR") THEN USD = TRN_AMT/22727.50;
	ELSE IF CRD_CLNT_ID IN ("SC_C400ZM_CR") THEN USD = TRN_AMT/8.98193;
	ELSE IF CRD_CLNT_ID IN ("SC_CCMSHK_CR") THEN USD = TRN_AMT/7.8;
	ELSE IF CRD_CLNT_ID IN ("SC_C400BD_CR") THEN USD = TRN_AMT_LOCAL/83.60;

DROP RULE_NAME_STRG;
IF DECI_CD = 'decline';
RUN;

PROC SQL;
CREATE TABLE SUMM1 AS 
SELECT MER_ID, COUNT(DISTINCT MER_NM) AS NUM_MERCHANTS,
COUNT(DISTINCT FI_TRANSACTION_ID) AS NUM_DECLINES,
SUM(CASE WHEN FRAUD=1 THEN 1 ELSE 0 END) AS NUM_FRD,
SUM(CASE WHEN FRAUD=1 THEN usd ELSE 0 END) AS USD_FRD FORMAT = DOLLAR10.

FROM RULE_PERFORMENCE_CR_1
GROUP BY MER_ID
ORDER BY NUM_DECLINES DESC;
QUIT;

PROC SQL;
SELECT COUNT(DISTINCT FI_TRANSACTION_ID) INTO :TOTAL
FROM RULE_PERFORMENCE_CR_1;
QUIT;

%PUT &TOTAL.;
OPTIONS OBS=20;
DATA SUMM1;
SET SUMM1;
FORMAT DECLINE_CONTRI PERCENT10.2;
DECLINE_CONTRI = NUM_DECLINES / &TOTAL.;
FPR = NUM_DECLINES;
IF NUM_FRD > 0 THEN FPR = NUM_DECLINES / NUM_FRD;
RUN;
OPTIONS OBS=MAX;


/*COMMENTS: CHANGE PATH*/

PROC EXPORT DATA=SUMM1 OUTFILE="C:\Users\1510806\Desktop\Jhilam\BLACKLIST_MID_CR &mnth1..XLSX"
DBMS=XLSX REPLACE;
SHEET="SUMMARY";
RUN;

PROC EXPORT DATA=RULE_PERFORMENCE_CR_1 OUTFILE="C:\Users\1510806\Desktop\Jhilam\BLACKLIST_MID_CR &mnth1..XLSX"
DBMS=XLSX REPLACE;
SHEET="DETAILS";
RUN;