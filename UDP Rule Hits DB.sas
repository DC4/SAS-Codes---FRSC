

PROC DELETE DATA = RULE_PERFORMENCE_DB; RUN;

****Place the updated FRD15 dataset inside the below mentioned folder***********************;

/*COMMENTS: CHANGE PATH*/

LIBNAME EXIST "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Falcon 6.4 Monthly Data";

OPTIONS COMPRESS=YES;


 %LET TOD = %SYSFUNC(TODAY());
 %LET MNTH1 = %SYSFUNC(INTNX(MONTH,&TOD.,-1),MONYY7.);
 %LET MNTH2 = %SYSFUNC(INTNX(MONTH,&TOD.,-2),MONYY7.);
 %LET MNTH3 = %SYSFUNC(INTNX(MONTH,&TOD.,-3),MONYY7.);
 %LET MNTH_1 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH1.,1,3)),%SYSFUNC(SUBSTR(&MNTH1.,6,2)) ));
 %LET MNTH_2 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH2.,1,3)),%SYSFUNC(SUBSTR(&MNTH2.,6,2)) ));
 %LET MNTH_3 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH3.,1,3)),%SYSFUNC(SUBSTR(&MNTH3.,6,2)) ));
 %PUT &MNTH1. &MNTH2. &MNTH3. &MNTH_1. &MNTH_2. &MNTH_3.;



**********Change the date as per requirement (which all dates need to append)*******************;

OPTIONS MPRINT MLOGIC SYMBOLGEN;

%MACRO DEC(CNTY);

/*COMMENTS: CHANGE PATH*/

LIBNAME DEBIT "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Falcon 6.4 Monthly Data\debit";

DATA FALT003;
	SET DEBIT.FALT003_&CNTY._DB_&MNTH1.(WHERE=(STRIP(SUBSTR(CLIENT_XID,LENGTH(CLIENT_XID)-4))="&CNTY._DB" AND 
										UPCASE(SUBSTR(CREATED_DTTM,4,6)) IN ("&MNTH_1." ) ))
		DEBIT.FALT003_&CNTY._DB_&MNTH2.(WHERE=(STRIP(SUBSTR(CLIENT_XID,LENGTH(CLIENT_XID)-4))="&CNTY._DB" AND 
										UPCASE(SUBSTR(CREATED_DTTM,4,6)) IN ("&MNTH_2." ) ))
		DEBIT.FALT003_&CNTY._DB_&MNTH3.(WHERE=(STRIP(SUBSTR(CLIENT_XID,LENGTH(CLIENT_XID)-4))="&CNTY._DB" AND 
										UPCASE(SUBSTR(CREATED_DTTM,4,6)) IN ("&MNTH_3." ) ));
	COUNTRY = UPCASE(STRIP(SUBSTR(CLIENT_XID,LENGTH(CLIENT_XID)-4)));
RUN;

PROC SORT DATA=FALT003 OUT=RULE_&CNTY. NODUPKEY; BY FI_TRANSACTION_ID RULE_NAME_STRG ; RUN;

PROC SQL;
	CREATE TABLE FALT002 AS SELECT * FROM 

	((SELECT *
	FROM DEBIT.FALT002_&CNTY._DB_&MNTH1. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._DB" AND 
	TRN_AUTH_POST='A' AND UPCASE(SUBSTR(TRN_DT,4,6)) IN ("&MNTH_1." ) )) OUTER UNION CORR

	(SELECT *
	FROM DEBIT.FALT002_&CNTY._DB_&MNTH2. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._DB" AND 
	TRN_AUTH_POST='A' AND UPCASE(SUBSTR(TRN_DT,4,6)) IN ("&MNTH_2." ) )) OUTER UNION CORR

	(SELECT *
	FROM DEBIT.FALT002_&CNTY._DB_&MNTH3. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._DB" AND 
	TRN_AUTH_POST='A' AND UPCASE(SUBSTR(TRN_DT,4,6)) IN ("&MNTH_3." ) )) );
QUIT;

PROC SORT DATA=FALT002 OUT=TXN_&CNTY.; BY FI_TRANSACTION_ID DESCENDING FRD_IND;  RUN;
PROC SORT DATA=TXN_&CNTY. NODUPKEY; BY FI_TRANSACTION_ID;  RUN;

PROC SQL;
	CREATE TABLE TXN_DETAILS_&CNTY. AS SELECT * FROM RULE_&CNTY. AS A LEFT JOIN TXN_&CNTY. AS B ON 
	A.SCORE_CUSTOMER_ACCOUNT_XID=B.ACCT_NBR AND
	A.FI_TRANSACTION_ID=B.FI_TRANSACTION_ID;
QUIT;

PROC SORT DATA=EXIST.FRD15 OUT=FRD_&CNTY. NODUPKEY; 
	WHERE STRIP(SUBSTR(CLIENTIDFROMHEADER,LENGTH(CLIENTIDFROMHEADER)-4))="&CNTY._DB"; 
	BY PAN FITRANSACTIONIDREFERENCE; 
RUN;

PROC SQL;
CREATE TABLE TXN_DETAILS_FRD_&CNTY. AS
		SELECT A.*,
		  SUBSTR(CREATED_DTTM,1,9) AS DATE,
		  UPCASE(SUBSTR(CREATED_DTTM,4,6)) AS MONTH,
		  A.TRN_AMT AS AMOUNT,
		  B.FRAUDFLAG,
		  CASE 
			WHEN STRIP(UPCASE(A.FRD_IND)) IN ("Y" "D") OR B.FRAUDFLAG="1" THEN 1 
		  END AS FRAUD
FROM TXN_DETAILS_&CNTY. AS A LEFT JOIN FRD_&CNTY. AS B ON 
A.ACCT_NBR = B.PAN AND
A.FI_TRANSACTION_ID=B.FITRANSACTIONIDREFERENCE;
QUIT;

PROC APPEND DATA=TXN_DETAILS_FRD_&CNTY. BASE=RULE_PERFORMENCE_DB FORCE; RUN;

%MEND;

%DEC(AE);
%DEC(BD);
%DEC(BH);
%DEC(BN);
%DEC(BW);
%DEC(CI);
%DEC(CM);
%DEC(GH);
%DEC(GM);
%DEC(HK);
%DEC(ID);
%DEC(IN);
%DEC(JO);
%DEC(KE);
%DEC(LK);
%DEC(MY);
%DEC(NG);
%DEC(NP);
%DEC(QA);
%DEC(SG);
%DEC(SL);
%DEC(TZ);
%DEC(UG);
%DEC(VN);
%DEC(ZM);
%DEC(ZW);
%DEC(TW);


PROC SORT DATA=RULE_PERFORMENCE_DB NODUPKEY; BY FI_TRANSACTION_ID RULE_NAME_STRG; RUN;



DATA UDP_DB_APR19_JUN19;
LENGTH UDP_PROFILE $ 50.;
SET RULE_PERFORMENCE_DB;
WHERE UPCASE(RULE_NAME_STRG) LIKE "MR_BIN_ATTACK_%" OR 
	  UPCASE(RULE_NAME_STRG) LIKE "HR_BIN_ATTACK_%" OR 
	  UPCASE(RULE_NAME_STRG) LIKE "MR_MERCHANT_ACQID_%" OR
	  UPCASE(RULE_NAME_STRG) LIKE "HR_MERCHANT_ACQID_%";		
ACQ = INPUT(ACQUIRER_ID,BEST.);
ACQUIRER = STRIP(PUT(ACQ,Z11.));
MERCHANT = TRANWRD(MER_NM,STRIP(MER_ID),"");
COUNTRY = SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4,2);
IF SUBSTR(UPCASE(RULE_NAME_STRG),3,12) = "_BIN_ATTACK_" THEN DO;
	UDP_PROFILE = STRIP(MER_ID) || STRIP(SUBSTR(ACCT_NBR,1,6));
	UDP = "MER ID + ISSUER BIN";
END;
IF SUBSTR(UPCASE(RULE_NAME_STRG),3,16) = "_MERCHANT_ACQID_" THEN DO;
	UDP_PROFILE = STRIP(MER_ID) || STRIP(ACQUIRER);
	UDP = "MID + ACQID";
END;
RUN;



PROC FREQ DATA=UDP_DB_APR19_JUN19; TABLE MONTH*MER_ID*DECI_CD_ORIG
/NOCOL NOCUM NOPERCENT NOROW MISSING OUT=MID_UDP_DB_APR19_JUN19; 
RUN;


PROC SORT DATA=UDP_DB_APR19_JUN19; BY UDP UDP_PROFILE; RUN;


DATA UDP_DB_APR19_JUN19_N;
LENGTH MERCHANTS $ 500. DECI_CD_ORIG1 $ 5.;
SET UDP_DB_APR19_JUN19;
RETAIN MERCHANTS;
BY UDP UDP_PROFILE;
IF FIRST.UDP_PROFILE THEN MERCHANTS = MERCHANT;
ELSE IF INDEX(MERCHANTS,STRIP(MERCHANT))=0 THEN MERCHANTS = STRIP(MERCHANTS) || " | " || STRIP(MERCHANT);
IF DECI_CD_ORIG = "" THEN DECI_CD_ORIG1 = "Blank";
ELSE DECI_CD_ORIG1 = DECI_CD_ORIG;
DROP DECI_CD_ORIG;
RENAME DECI_CD_ORIG1 = DECI_CD_ORIG;
RUN;

PROC SQL;
CREATE TABLE UDP_DB_APR19_JUN19_SUMMARY AS 
SELECT DISTINCT UDP, MONTH, COUNTRY, MER_ID, UDP_PROFILE, 
		MERCHANTS,
		COUNT(DISTINCT FI_TRANSACTION_ID) AS TXN_COUNT, 
		COUNT(DISTINCT ACCT_NBR) AS ACCT_COUNT, 
		SUM(FRAUD) AS NO_OF_FRAUDS,
		COUNT(DISTINCT CASE WHEN FRAUD=1 THEN ACCT_NBR END) AS NO_OF_CARDS_FRAUD, 
		CASE 
		 WHEN SUM(FRAUD) NE . THEN CALCULATED TXN_COUNT/CALCULATED NO_OF_FRAUDS
		 ELSE CALCULATED TXN_COUNT
		END AS TFPR,
		CASE 
		 WHEN SUM(FRAUD) NE . THEN CALCULATED ACCT_COUNT/CALCULATED NO_OF_CARDS_FRAUD
		 ELSE CALCULATED ACCT_COUNT
		END AS AFPR,
		ROUND(CALCULATED ACCT_COUNT/90,1) AS IMPACT_PER_DAY
FROM UDP_DB_APR19_JUN19_N
GROUP BY UDP, MONTH, COUNTRY, MER_ID, UDP_PROFILE;
QUIT;

PROC SORT DATA=UDP_DB_APR19_JUN19_SUMMARY; BY UDP MONTH COUNTRY MER_ID UDP_PROFILE DESCENDING MERCHANTS; RUN;
PROC SORT DATA=UDP_DB_APR19_JUN19_SUMMARY NODUPKEY; BY UDP MONTH COUNTRY MER_ID UDP_PROFILE; RUN;

PROC SQL;
CREATE TABLE CCMS_DCISION AS 
SELECT DISTINCT UDP, MONTH, COUNTRY, MER_ID, UDP_PROFILE, DECI_CD_ORIG,
COUNT(DISTINCT FI_TRANSACTION_ID) AS COUNT
FROM UDP_DB_APR19_JUN19_N
GROUP BY UDP, MONTH, COUNTRY, MER_ID, UDP_PROFILE, DECI_CD_ORIG;
QUIT;

PROC SORT DATA=CCMS_DCISION NODUPKEY; BY UDP MONTH COUNTRY MER_ID UDP_PROFILE; RUN;

PROC TRANSPOSE DATA=CCMS_DCISION OUT=CCMS_DCISION_UDP PREFIX=CCMS_DCISION_;
BY UDP MONTH COUNTRY MER_ID UDP_PROFILE;
VAR COUNT;
ID DECI_CD_ORIG;
RUN;





**** COMMENTS: DOWNLOAD THE BELOW FIRST 4 HOTLIST FROM CASE MANAGER LAST FILE TAKE FROM FSC_MIS TEAM (SUBBU) *********;


PROC IMPORT DATAFILE="C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Required\Hotlist\BLACKLISTED_MERCHANTS_DEBIT_RAC_INDIA.xlsx" OUT=BM_DEBIT_RAC_INDIA DBMS=XLSX REPLACE;
RUN;
PROC IMPORT DATAFILE="C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Required\Hotlist\BLACKLISTED_MERCHANTS_DEBIT_RAC_SG.xlsx" OUT=BM_DEBIT_RAC_SG DBMS=XLSX REPLACE;
RUN;
PROC IMPORT DATAFILE="C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Required\Hotlist\BLACKLIST_MIDS_BINATTACK_DEBIT.xlsx" OUT=BINATTACK_DEBIT DBMS=XLSX REPLACE;
RUN;
PROC IMPORT DATAFILE="C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Required\Hotlist\BLACKLIST_MIDS_COUNTRYWISE_DEBIT.xlsx" OUT=COUNTRYWISE_DEBIT DBMS=XLSX REPLACE;
RUN;
PROC IMPORT DATAFILE="C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Required\Hotlist\MIDs_Blacklisted _and_Removed.xlsx" OUT=BM0 DBMS=XLSX REPLACE;
SHEET="ADDED";
RUN;

PROC SQL;
CREATE TABLE BM_DEBIT_RAC_INDIA1 AS
SELECT * FROM BM_DEBIT_RAC_INDIA WHERE NAME NOT IN (SELECT DISTINCT STRIP(MID) FROM BM0);
QUIT;
PROC SQL;
CREATE TABLE BM_DEBIT_RAC_SG1 AS
SELECT * FROM BM_DEBIT_RAC_SG WHERE NAME NOT IN (SELECT DISTINCT STRIP(MID) FROM BM0);
QUIT;

DATA BM1;
SET BM0(KEEP=MID BLACKLISTED_FOR)
	BM_DEBIT_RAC_INDIA1(RENAME=(NAME=MID COUNTRY=BLACKLISTED_FOR) KEEP=NAME COUNTRY)
	BM_DEBIT_RAC_SG1(RENAME=(NAME=MID COUNTRY=BLACKLISTED_FOR) KEEP=NAME COUNTRY);
IF MID NE "" AND INDEX(UPCASE(BLACKLISTED_FOR),"_CR") = 0;
RUN;

PROC SORT DATA=BM1; BY MID; RUN;

DATA BM_1(DROP=BLACKLISTED_FOR);
SET BM1;
BY MID;
RETAIN BLACKLIST_FOR;
IF FIRST.MID THEN BLACKLIST_FOR = BLACKLISTED_FOR;
ELSE BLACKLIST_FOR = STRIP(BLACKLIST_FOR) || ", " || STRIP(BLACKLISTED_FOR);
IF LAST.MID;
RUN;


PROC SQL;
CREATE TABLE BINATTACK_DEBIT1 AS
SELECT * FROM BINATTACK_DEBIT WHERE NAME NOT IN (SELECT DISTINCT MID FROM BM_1);
QUIT;
PROC SQL;
CREATE TABLE COUNTRYWISE_DEBIT1 AS
SELECT * FROM COUNTRYWISE_DEBIT WHERE NAME NOT IN (SELECT DISTINCT MID FROM BM_1);
QUIT;

DATA BM2;
SET BINATTACK_DEBIT1(RENAME=(NAME=MID VALUE=BLACKLISTED_FOR) KEEP=NAME VALUE)
	COUNTRYWISE_DEBIT1(RENAME=(NAME=MID VALUE=BLACKLISTED_FOR) KEEP=NAME VALUE);
RUN;

PROC SORT DATA=BM2; BY MID; RUN;

DATA BM_2(DROP=BLACKLISTED_FOR);
SET BM2;
BY MID;
RETAIN BLACKLIST_FOR;
IF FIRST.MID THEN BLACKLIST_FOR = BLACKLISTED_FOR;
ELSE BLACKLIST_FOR = STRIP(BLACKLIST_FOR) || ", " || STRIP(BLACKLISTED_FOR);
IF LAST.MID;
RUN;

DATA BM;
SET BM_1 BM_2;
RUN;

PROC SQL;
CREATE TABLE UDP_RULE_HITS_SUMMARY(DROP=_NAME_) AS 
SELECT * FROM 
(
SELECT X.*, Y.Blacklist_for
FROM 
(SELECT *, CASE WHEN MER_ID IN 
(SELECT DISTINCT MID FROM BM WHERE MID NE "") 
THEN "Y" END AS BLACKLISED
FROM UDP_DB_APR19_JUN19_SUMMARY) AS X
LEFT JOIN
BM AS Y
ON STRIP(X.MER_ID) = STRIP(Y.MID)
)
AS A
LEFT JOIN CCMS_DCISION_UDP AS B
ON STRIP(A.UDP) = STRIP(B.UDP) AND 
STRIP(A.MONTH) = STRIP(B.MONTH) AND 
STRIP(A.COUNTRY) = STRIP(B.COUNTRY) AND 
STRIP(A.MER_ID) = STRIP(B.MER_ID) AND 
STRIP(A.UDP_PROFILE) = STRIP(B.UDP_PROFILE) ;
QUIT;


/*COMMENTS: CHANGE PATH*/

PROC EXPORT DATA=UDP_RULE_HITS_SUMMARY OUTFILE="C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\DB UDP_RULE_HITS_SUMMARY.xlsx" DBMS=XLSX REPLACE;
RUN;





/*PROC SQL;*/
/*CREATE TABLE UDP_RULE_HITS_SUMMARY AS */
/*SELECT * FROM */
/*(SELECT *, CASE WHEN MER_ID IN */
/*(SELECT DISTINCT MID FROM BM WHERE MID NE "") */
/*THEN "Y" END AS BLACKLISED*/
/*FROM UDP_DB_APR19_JUN19_SUMMARY) AS A*/
/*LEFT JOIN CCMS_DCISION_UDP AS B*/
/*ON STRIP(A.UDP) = STRIP(B.UDP) AND */
/*STRIP(A.MONTH) = STRIP(B.MONTH) AND */
/*STRIP(A.COUNTRY) = STRIP(B.COUNTRY) AND */
/*STRIP(A.MER_ID) = STRIP(B.MER_ID) AND */
/*STRIP(A.UDP_PROFILE) = STRIP(B.UDP_PROFILE) ;*/
/*QUIT;*/