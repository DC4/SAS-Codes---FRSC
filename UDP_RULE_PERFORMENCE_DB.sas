

PROC DELETE DATA = RULE_PERFORMENCE_DB; RUN;

****Place the updated FRD15 dataset inside the below mentioned folder***********************;


/*COMMENTS: CHANGE PATH*/

LIBNAME EXIST "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Falcon 6.4 Monthly Data";

OPTIONS COMPRESS=YES;

 %LET TOD = %SYSFUNC(TODAY());
 %LET MNTH1 = %SYSFUNC(INTNX(MONTH,&TOD.,-1),MONYY7.);
 %LET MNTH2 = %SYSFUNC(INTNX(MONTH,&TOD.,-2),MONYY7.);
 %LET MNTH3 = %SYSFUNC(INTNX(MONTH,&TOD.,-3),MONYY7.);
 %LET MNTH_1 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH1.,1,3)),%SYSFUNC(SUBSTR(&MNTH1.,6,2)) ));
 %LET MNTH_2 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH2.,1,3)),%SYSFUNC(SUBSTR(&MNTH2.,6,2)) ));
 %LET MNTH_3 = %SYSFUNC(CATX(-,%SYSFUNC(SUBSTR(&MNTH3.,1,3)),%SYSFUNC(SUBSTR(&MNTH3.,6,2)) ));
 %PUT &MNTH1. &MNTH2. &MNTH3. &MNTH_1. &MNTH_2. &MNTH_3.;




/*RULE PERFORMANCE DATA*/


PROC DELETE DATA=UDP_RULE_PERFORMENCE_DB; RUN;


 **********Change the date as per requirement (which all dates need to append)*******************;

OPTIONS MPRINT MLOGIC SYMBOLGEN;

%MACRO DEC(CNTY);


/*COMMENTS: CHANGE PATH*/

LIBNAME DEBIT "C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\Falcon 6.4 Monthly Data\debit";

DATA FALT003;
	SET DEBIT.FALT003_&CNTY._DB_&MNTH1.
		DEBIT.FALT003_&CNTY._DB_&MNTH2.
		DEBIT.FALT003_&CNTY._DB_&MNTH3.;
	COUNTRY = UPCASE(STRIP(SUBSTR(CLIENT_XID,LENGTH(CLIENT_XID)-4)));
	WHERE UPCASE(RULE_NAME_STRG) LIKE "MR_BIN_ATTACK_%" OR 
	  UPCASE(RULE_NAME_STRG) LIKE "HR_BIN_ATTACK_%" OR 
	  UPCASE(RULE_NAME_STRG) LIKE "MR_MERCHANT_ACQID_%" OR
	  UPCASE(RULE_NAME_STRG) LIKE "HR_MERCHANT_ACQID_%";
RUN;

PROC SORT DATA=FALT003 OUT=RULE_&CNTY. NODUPKEY; BY FI_TRANSACTION_ID RULE_NAME_STRG ; RUN;

PROC SQL;
	CREATE TABLE FALT002 AS SELECT * FROM 

	((SELECT * FROM DEBIT.FALT002_&CNTY._DB_&MNTH1. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._DB" AND 
	TRN_AUTH_POST='A' AND UPCASE(SUBSTR(TRN_DT,4,6)) IN ("&MNTH_1." ) )) OUTER UNION CORR

	(SELECT * FROM DEBIT.FALT002_&CNTY._DB_&MNTH2. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._DB" AND 
	TRN_AUTH_POST='A' AND UPCASE(SUBSTR(TRN_DT,4,6)) IN ("&MNTH_2." ) )) OUTER UNION CORR

	(SELECT * FROM DEBIT.FALT002_&CNTY._DB_&MNTH3. WHERE 
	FI_TRANSACTION_ID IN (SELECT DISTINCT FI_TRANSACTION_ID FROM RULE_&CNTY.) AND 
	(STRIP(SUBSTR(CRD_CLNT_ID,LENGTH(CRD_CLNT_ID)-4))="&CNTY._DB" AND 
	TRN_AUTH_POST='A' AND UPCASE(SUBSTR(TRN_DT,4,6)) IN ("&MNTH_3." ) )) );
QUIT;

PROC SORT DATA=FALT002 OUT=TXN_&CNTY.; BY FI_TRANSACTION_ID DESCENDING FRD_IND;  RUN;
PROC SORT DATA=TXN_&CNTY. NODUPKEY; BY FI_TRANSACTION_ID;  RUN;

PROC SQL;
	CREATE TABLE TXN_DETAILS_&CNTY. AS SELECT * FROM RULE_&CNTY. AS A LEFT JOIN TXN_&CNTY. AS B ON 
	A.SCORE_CUSTOMER_ACCOUNT_XID=B.ACCT_NBR AND
	A.FI_TRANSACTION_ID=B.FI_TRANSACTION_ID;
QUIT;

PROC SORT DATA=EXIST.FRD15 OUT=FRD_&CNTY. NODUPKEY; 
	WHERE STRIP(SUBSTR(CLIENTIDFROMHEADER,LENGTH(CLIENTIDFROMHEADER)-4))="&CNTY._DB"; 
	BY PAN FITRANSACTIONIDREFERENCE; 
RUN;

PROC SQL;
CREATE TABLE TXN_DETAILS_FRD_&CNTY. AS
		SELECT A.*,
		  SUBSTR(CREATED_DTTM,1,9) AS DATE,
		  UPCASE(SUBSTR(CREATED_DTTM,4,6)) AS MONTH,
		  A.TRN_AMT AS AMOUNT,
		  B.FRAUDFLAG,
		  CASE 
			WHEN STRIP(UPCASE(A.FRD_IND)) IN ("Y" "D") OR B.FRAUDFLAG="1" THEN 1 
		  END AS FRAUD
FROM TXN_DETAILS_&CNTY. AS A LEFT JOIN FRD_&CNTY. AS B ON 
A.ACCT_NBR = B.PAN AND
A.FI_TRANSACTION_ID=B.FITRANSACTIONIDREFERENCE;
QUIT;

PROC APPEND DATA=TXN_DETAILS_FRD_&CNTY. BASE=UDP_RULE_PERFORMENCE_DB FORCE; RUN;

%MEND;

%DEC(AE);
%DEC(BD);
%DEC(BH);
%DEC(BN);
%DEC(BW);
%DEC(CI);
%DEC(CM);
%DEC(GH);
%DEC(GM);
%DEC(HK);
%DEC(ID);
%DEC(IN);
%DEC(JO);
%DEC(KE);
%DEC(LK);
%DEC(MY);
%DEC(NG);
%DEC(NP);
%DEC(QA);
%DEC(SG);
%DEC(SL);
%DEC(TZ);
%DEC(UG);
%DEC(VN);
%DEC(ZM);
%DEC(ZW);
%DEC(TW);

PROC SORT DATA=UDP_RULE_PERFORMENCE_DB NODUPKEY; BY FI_TRANSACTION_ID RULE_NAME_STRG; RUN;


/*COMMENTS: CHANGE PATH*/

PROC EXPORT DATA=UDP_RULE_PERFORMENCE_DB OUTFILE="C:\Users\1510806\OneDrive - Standard Chartered Bank\Desktop\Jhilam\UDP_RULE_PERFORMENCE_DB.xlsx" DBMS=XLSX REPLACE;
RUN;